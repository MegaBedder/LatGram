#!/usr/bin/latino
config = incluir ('ajustes')
URL = 'https://api.telegram.org/bot' . config->api_key . '/'
aURL = 'https://api.telegram.org/file/bot' . config->api_key . '/'
html = "html"
md = "markdown"
incluir ('metodos')
incluir ('utilidades')

bot = ObtenerBot()
bot = bot->result
bot = {
	"id": bot->id,
	"nombre": bot->first_name,
	"usuario": bot->username
}

bot_shash = "bot:".bot->id.":schats"
verificacion = verificar_apikey()
si ! verificacion->ok
	imprimir("\aERROR (".verificacion->error_code." - ".verificacion->description.")\n\nVerifica la apikey en ajustes.lat", 1)
	retorno 0
fin

com = redis->conectar(config->redis_ip, config->redis_puerto)

inicio = enviarMensaje(config->admin, "<b>Bot iniciado</b>", html, falso)
si inicio
	imprimir("Confirmación enviada a " . config->admin . ".\nBot @".bot->usuario, 1)
sino
	imprimir("Error en confirmación.\nBot @".bot->usuario, 1)
fin

si config->limpiar_updates_al_iniciar
	updates = limpiar_updates()
	si updates
		imprimir("Actualizaciones limpias con un total de ".updates."\n", 1)
	fin
fin

plugins = cargar_plugins()

funcion variables()
	res = res->result[0]
	msj = res->message
	msg = {
		"id": msj->id,
		"texto": msj->text,
		"fecha": msj->date,
		"de": {
			"id": msj->from->id,
			"nombre": msj->from->first_name,
			"apellido": msj->from->last_name,
			"usuario": msj->from->username
		},
		"chat": {
			"id": msj->chat->id,
			"titulo": msj->chat->title,
			"nombre": msj->chat->first_name,
			"apellido": msj->chat->last_name,
			"usuario": msj->chat->username,
			"tipo": msj->chat->type
		},
		"agregado": {
			"id": msj->new_chat_member->id,
			"usuario": msj->new_chat_member->username,
			"nombre": msj->new_chat_member->first_name,
			"apellido": msj->new_chat_member->last_name
		},
		"eliminado": {
			"id": msj->left_chat_member->id,
			"usuario": msj->left_chat_member->username,
			"nombre": msj->left_chat_member->first_name,
			"apellido": msj->left_chat_member->last_name
		}
	}

	respuesta = msj->reply_to_message
	respuesta = {
		"texto": respuesta->text,
		"fecha": respuesta->date,
		"de": {
			"tipo": respuesta->chat->type,
			"id": respuesta->chat->id,
			"usuario": respuesta->chat->username,
			"nombre": respuesta->chat->first_name,
			"apellido": respuesta->chat->last_name
		},
		"sticker": {
			"id": respuesta->sticker->file_id,
			"thumb": {
				"id": respuesta->sticker->thumb->file_id,
				"peso": respuesta->sticker->thumb->file_size,
				"anchura": respuesta->sticker->thumb->width,
				"altura": respuesta->sticker->thumb->height
			},
			"peso": respuesta->sticker->file_size,
			"emoji": respuesta->sticker->emoji,
			"anchura": respuesta->sticker->width,
			"altura": respuesta->sticker->height
		},
		"msg_id": respuesta->message_id,
		"a": {
			"id": respuesta->from->id,
			"usuario": respuesta->from->username,
			"first_name": respuesta->from->first_name
		}
	}

	sticker = msj->sticker
	sticker = {
		"anchura": sticker->width,
		"altura": sticker->height,
		"emoji": sticker->emoji,
		"thumb": {
			"id": sticker->thumb->file_id,
			"peso": sticker->thumb->file_size,
			"anchura": sticker->thumb->width,
			"altura": sticker->thumb->height
		},
		"id": sticker->file_id,
		"peso": sticker->file_size
	}

	cinline = res->inline_query
	cinline = {
		"id": cinline->id,
		"texto": cinline->query,
		"offset": cinline->offset,
		"de": {
			"id": cinline->from->id,
			"nombre": cinline->from->first_name,
			"apellido": cinline->from->last_name,
			"usuario": cinline->from->username
		}
	}
fin

funcion iniciar_bot()
	si config->ires ires() fin
	desde (num_plugin=0; num_plugin<plugins; num_plugin++)
		si convertir->cadena(plugins[num_plugin]->subproceso) == "funcion" // convertimos a cadena y obligamos al usuario a usar una función
			retorna = plugins[num_plugin]->subproceso()
			si retorna imprimir("\nSubproceso: ".retorna, 1) fin
		fin
		si plugins[num_plugin]->comandos
			desde (num_comando=0; num_comando<(plugins[num_plugin]->comandos); num_comando++)
				si msg->texto
					matches = cadena->match(msg->texto, plugins[num_plugin]->comandos[num_comando])
					si matches
						matches = matches[0]
						blocks = matches
						si convertir->cadena(plugins[num_plugin]->accion) == "funcion"
							plugins[num_plugin]->accion()
							imprimir("\nComando ejecutado: ".matches[0]."\nUsuario: ".msg->de->nombre."\nID: ".msg->de->id, 1)
						fin
					fin
				fin
			fin
		fin
		si plugins[num_plugin]->icomandos
			desde (num_icomando=0; num_icomando<(plugins[num_plugin]->icomandos); num_icomando++)
				si cinline->texto
					imatches = cadena->match(cinline->texto, plugins[num_plugin]->icomandos[num_icomando])
					si imatches
						imatches = imatches[0]
						iblocks = imatches
						si convertir->cadena(plugins[num_plugin]->inline) == "funcion"
							plugins[num_plugin]->inline()
							imprimir("\nConsulta inline: ".imatches[0]."\nUsuario: ".cinline->de->usuario, 1)
						fin
					fin
				fin
			fin
		fin
	fin
fin

mientras verdadero
	res = actualizaciones()
	si res
		variables()
		iniciar_bot()
	fin
fin

redis->desconectar(com)
