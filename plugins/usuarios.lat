funcion inline()
    cinline_datos = "\nID: "..cinline.de.id.."\nNombre: "..cinline.de.nombre
    si cinline.de.apellido
        cinline_datos = cinline_datos.."\nApellido: "..cinline.de.apellido
    fin
    si cinline.de.usuario
        cinline_datos = cinline_datos.."\nUsuario: @"..cinline.de.usuario
    fin
    array = [
      textoInline(1, "Tu información de usuario", cinline_datos),
      textoInline(2, "Tu ID", "`"..cinline.de.id.."`", falso, md)
      ]
      imprimir(array)
    enviarConsultaInline(cinline.id, array, 1)
fin

funcion callback()
   si cmatches[1] == "latino"
      link_teclado = [[
      {"text":"Github","url":"https://github.com/lenguaje-latino/BotGram"}
      ]]
      editarMensaje(cb.msg.chat.id, cb.msg.id, "*Latino* es un lenguaje de programación en español interpretado hecho en C"
      .."\n¿Puedo ayudar? Si así es, *Latino busca desarrolladores* para Latino."
      .."\nCabe destacar que este bot *fué programado en Latino*.", md, link_teclado)
   fin
fin

funcion mensajes()
   si matches[1] == "id"
      si !matches[2]
         enviarMensaje(msg.chat.id, cadena.formato(leng.usuarios[0].id, msg.chat.id, msg.de.id), md)
         retornar 0
      fin
      id_obtenida = obtener_id_usuario(matches[2])
      si id_obtenida
         enviarMensaje(msg.chat.id, "`"..id_obtenida.."`", md)
      sino
         enviarMensaje(msg.chat.id, leng.error[0], md)
      fin
   fin
   si matches[1] == "start"
      ayuda = [[
      {"text":"Programación Latino","callback_data":"!!!latino"},
      {"text":"Repositorio del Bot","url":"https://github.com/lenguaje-latino/BotGram"}
      ]]
      enviarTeclado(msg.chat.id, cadena.formato(leng.usuarios[0].start, no_markdown(msg.de.nombre), no_markdown(bot.usuario)), ayuda, md)
   fin
fin

retornar {
   "comandos": [
      "^/(id)$",
      "^/(id) @([[:alnum:]_]+)$",
      "^/(id)@"..bot.usuario.."$",
      "^/(start)$",
      "^/(start)@"..bot.usuario.."$"
   ],

   "icomandos": [
      "^id$"
   ],

   "callbacks": [
      "^!!!(latino)$"
   ],

   "accion": mensajes,
   "inline": inline,
   "callback": callback
}
